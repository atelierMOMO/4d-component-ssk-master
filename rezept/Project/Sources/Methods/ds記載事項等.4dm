//%attributes = {"invisible":true,"shared":true,"preemptive":"capable"}
C_BOOLEAN:C305($1; $noHost)
C_OBJECT:C1216($0; $EXPORT)

If (Count parameters:C259#0)
	$noHost:=$1
Else 
	$noHost:=(Folder:C1567(fk database folder:K87:14).platformPath=Folder:C1567(fk database folder:K87:14; *).platformPath)
End if 

$dataClassName:="記載事項等"

If ($noHost)
	
	$DEBUG_DIRECT_LINK:=False:C215
	$DEBUG_CREATE_DATA:=True:C214
	
	//データ再構築
	If (Storage:C1525[$dataClassName]=Null:C1517)
		Use (Storage:C1525)
			$診療行為コード:=New shared object:C1526
			$診療行為名:=New shared object:C1526
			$記載事項等:=New shared collection:C1527
			
			//$フリー:=New shared collection
			//$検査の実施日:=New shared collection
			//$算定日:=New shared collection
			//$メッセージ:=New shared collection
			
			$EXPORT:=New shared object:C1526
			Storage:C1525[$dataClassName]:=$EXPORT
			Use ($EXPORT)
				$EXPORT.診療行為コード:=$診療行為コード
				If ($DEBUG_DIRECT_LINK)
					$EXPORT.診療行為名:=$診療行為名
				End if 
				$EXPORT.記載事項等:=$記載事項等
				//$EXPORT.フリー:=$フリー
				//$EXPORT.検査の実施日:=$検査の実施日
				//$EXPORT.算定日:=$算定日
				//$EXPORT.メッセージ:=$メッセージ
			End use 
		End use 
	Else 
		$EXPORT:=Storage:C1525[$dataClassName]
		$診療行為コード:=$EXPORT.診療行為コード
		If ($DEBUG_DIRECT_LINK)
			$診療行為名:=$EXPORT.診療行為名
		End if 
		$記載事項等:=$EXPORT.記載事項等
	End if 
	
	If ($記載事項等.length=0)
		
		$dataFolder:=Folder:C1567("/RESOURCES")
		$files:=$dataFolder.files()
		$names:=New collection:C1472("摘要欄への記載事項等一覧@"; "記載事項等")
		$files:=$files.query("name in :1 and extension == :2"; $names; ".xlsx")
		
		If ($files.length#0)
			
			$file:=$files[0]
			//%W-533.4
			$json:=XLSX TO JSON($file.platformPath)
			//%W+533.4
			C_OBJECT:C1216($data)
			
			$data:=JSON Parse:C1218($json; Is object:K8:27)
			
			$sheets:=$data.sheets
			
			If ($sheets.length>0)
				
				$悪性腫瘍組織検査１:=load_collection("悪性腫瘍組織検査１")
				$悪性腫瘍遺伝子検査:=load_collection("悪性腫瘍遺伝子検査")
				$障害者施設等入院基本料:=load_collection("障害者施設等入院基本料")
				$一般病棟入院基本料:=load_collection("一般病棟入院基本料")
				$精神病棟入院基本料:=load_collection("精神病棟入院基本料")
				$特定機能病院入院基本料:=load_collection("特定機能病院入院基本料")
				$算定回数が複数月に１回のみとされている検査:=load_collection("算定回数が複数月に１回のみとされている検査")
				
				var $rezept : cs:C1710.rezept
				$rezept:=cs:C1710.rezept.new()
				
				$codes:=New collection:C1472
				
				C_LONGINT:C283($index; $subIndex)
				
				For each ($row; $sheets[0].rows; 1)
					
					C_OBJECT:C1216($記載事項)
					
					$values:=$row.values
					
					If ($values.join()#"")
						
						ARRAY LONGINT:C221($pos; 0)
						ARRAY LONGINT:C221($len; 0)
						
						If ($values.length>5)
							$meta:=$values[5]  //F
						Else 
							$meta:=""
						End if 
						
						Case of 
							: ($meta="@*@")  //前の事項の条件のつづき（$記載事項をインクリメントしない）
								$index:=0
							Else 
								$index:=1
						End case 
						
						If ($values.length>6)
							$meta:=$values[6]  //G
						Else 
							$meta:=""
						End if 
						
						Case of 
							: ($meta="@*@")  //前のコメントのつづき
								$subIndex:=0
								$index:=0
							Else 
								$subIndex:=1
						End case 
						
						C_OBJECT:C1216($o)
						
						If ($index=1)
							
							$o:={}
							$o.項番:=$values[0]  //A
							$o.区分:=$values[1]  //B
							$o.診療行為名称等:=$values[2]  //C
							
							$診療行為名称等:=$o.診療行為名称等
							
							//マスターに合わせる
							Case of 
								: ($診療行為名称等="薬剤管理指導料@")
									$診療行為名称等:="薬剤管理指導料"
								: ($診療行為名称等="コンピューター断層撮影診断料@")
									$診療行為名称等:="コンピューター断層撮影診断"
								: ($診療行為名称等="点滴注射の血漿成分製剤加算@")
									$診療行為名称等:="血漿成分製剤加算（点滴注射）"
								: ($診療行為名称等="注入器用注射針加算の１@")
									$診療行為名称等:="注入器用注射針加算（１型糖尿病、血友病患者又はこれらの患者に準ずる状態にある患者）"
								: ($診療行為名称等="SARS-CoV-2抗原検出（定性）")
									$診療行為名称等:="ＳＡＲＳ−ＣｏＶ−２抗原検出（定性）"
								: ($診療行為名称等="SARS-CoV-2抗原検出（定量）")
									$診療行為名称等:="ＳＡＲＳ−ＣｏＶ−２抗原検出（定量）"
								: ($診療行為名称等="SARS-CoV-2・インフルエンザウイルス抗原同時検出（定性）")
									$診療行為名称等:="ＳＡＲＳ−ＣｏＶ−２・インフルエンザウイルス抗原同時検出（定性）"
								: ($診療行為名称等="インターロイキン－６（ＩＬ－６）")
									$診療行為名称等:="インターロイキン−６（ＩＬ−６）"
								: ($診療行為名称等="ｓＦｌｔ－１／ＰｌＧＦ比")
									$診療行為名称等:="ｓＦｌｔ−１／ＰｌＧＦ比"
								: ($診療行為名称等="SARS-CoV-2核酸検出")
									$診療行為名称等:="ＳＡＲＳ−ＣｏＶ−２核酸検出（検査委託"
								: ($診療行為名称等="ＲＯＳ１融合遺伝子検査")
									$診療行為名称等:="悪性腫瘍組織検査（処理が容易なもの）（医薬品の適応判定の補助等に用いるもの）（肺癌におけるＲＯＳ１融合遺伝子検査）"
								: ($診療行為名称等="ＡＬＫ融合遺伝子検査")
									$診療行為名称等:="悪性腫瘍組織検査（処理が容易なもの）（医薬品の適応判定の補助等に用いるもの）（肺癌におけるＡＬＫ融合遺伝子検査）"
								: ($診療行為名称等="ＮＴＲＫ融合遺伝子検査")
									$診療行為名称等:="悪性腫瘍組織検査（処理が複雑なもの）（固形癌におけるＮＴＲＫ融合遺伝子検査）"
								: ($診療行為名称等="遠隔連携診療料１")
									$診療行為名称等:="遠隔連携診療料（診断を目的とする場合）"
								: ($診療行為名称等="遠隔連携診療料２")
									$診療行為名称等:="遠隔連携診療料（その他の場合）"
								: ($診療行為名称等="肝エラストグラフィー加算")
									$診療行為名称等:="肝エラストグラフィ加算"
								: ($診療行為名称等="胚凍結保存管理料（導入時）")
									$診療行為名称等:="胚凍結保存管理料（胚凍結保存管理料（導入時））（"
								: ($診療行為名称等="胚凍結保存維持管理料")
									$診療行為名称等:="胚凍結保存管理料（胚凍結保存維持管理料）"
								: ($診療行為名称等="受精卵・胚培養管理料（注：胚盤胞の作成目的）")
									$診療行為名称等:="受精卵・胚培養管理料（"
								: ($診療行為名称等="高濃度ヒアルロン酸含有培養液を用いた前処置")
									$診療行為名称等:="高濃度ヒアルロン酸含有培養液加算"
								: ($診療行為名称等="精巣内精子採取術１単純なもの")
									$診療行為名称等:="精巣内精子採取術（単純なもの）"
								: ($診療行為名称等="精巣内精子採取術２顕微鏡を用いたもの")
									$診療行為名称等:="精巣内精子採取術（顕微鏡を用いたもの）"
								: ($診療行為名称等="移植後抗体関連型拒絶反応治療における血漿交換療法")
									$診療行為名称等:="血漿交換療法（１日につき）（移植後抗体関連型拒絶反応治療）"
								: ($診療行為名称等="SARS-CoV-2・インフルエンザ核酸同時検出")
									$診療行為名称等:="ＳＡＲＳ−ＣｏＶ−２・インフルエンザ核酸同時検出（検査委託"
								: ($診療行為名称等="感染症法による公費負担申請に係る診断書料及び協力料")
									$診療行為名称等:="感染症法による公費負担申請診断書交付・申請手続代行料"
								: ($診療行為名称等="在宅患者訪問看護・指導料、同一建物居住者訪問看護・指導料の夜間・早朝訪問看護加算")
									$診療行為名称等:="夜間・早朝訪問看護加算（在宅患者訪問看護・指導料、同一建物居住者訪問看護・指導料）"
								: ($診療行為名称等="在宅患者訪問看護・指導料、同一建物居住者訪問看護・指導料の深夜訪問看護加算")
									$診療行為名称等:="深夜訪問看護加算（在宅患者訪問看護・指導料、同一建物居住者訪問看護・指導料）"
								: ($診療行為名称等="在宅患者訪問看護・指導料、同一建物居住者訪問看護・指導料の在宅移行管理加算")
									$診療行為名称等:="在宅移行管理加算（在宅患者訪問看護・指導料、同一建物居住者訪問看護・指導料）"
								: ($診療行為名称等="在宅患者訪問看護・指導料、同一建物居住者訪問看護・指導料の在宅移行管理重症者加算")
									$診療行為名称等:="在宅移行管理重症者加算（在宅患者訪問看護・指導料、同一建物居住者訪問看護・指導料）"
								: ($診療行為名称等="在宅患者訪問看護・指導料、同一建物居住者訪問看護・指導料の看護・介護職員連携強化加算")
									$診療行為名称等:="看護・介護職員連携強化加算（在宅患者訪問看護・指導料、同一建物居住者訪問看護・指導料）"
								: ($診療行為名称等="在宅患者訪問看護・指導料、同一建物居住者訪問看護・指導料の特別地域訪問看護加算")
									$診療行為名称等:="特別地域訪問看護加算（在宅患者訪問看護・指導料、同一建物居住者訪問看護・指導料）"
							End case 
							
							While (Match regex:C1019("(の注\\d+.*)"; $診療行為名称等; 1; $pos; $len))
								$診療行為名称等:=Delete string:C232($診療行為名称等; $pos{1}; $len{1})
							End while 
							
							Case of 
								: ($診療行為名称等="1,500グラム未満の乳幼児加算")
								: ($診療行為名称等="1,500グラム未満の新生児加算")
								: ($診療行為名称等="1,500グラム未満の小児加算")
								Else 
									While (Match regex:C1019("(の.*加算(の[イロハニホヘト])?)"; $診療行為名称等; 1; $pos; $len))
										$診療行為名称等:=Delete string:C232($診療行為名称等; $pos{1}; $len{1})
									End while 
							End case 
							
							While (Match regex:C1019("(「\\d+」.*)"; $診療行為名称等; 1; $pos; $len))
								$診療行為名称等:=Delete string:C232($診療行為名称等; $pos{1}; $len{1})
							End while 
							
							While (Match regex:C1019("(「通則\\d+」.*)"; $診療行為名称等; 1; $pos; $len))
								$診療行為名称等:=Delete string:C232($診療行為名称等; $pos{1}; $len{1})
							End while 
							
							While (Match regex:C1019("(（(.*)場合）)"; $診療行為名称等; 1; $pos; $len))
								$診療行為名称等:=Delete string:C232($診療行為名称等; $pos{1}; $len{1})
							End while 
							
							While (Match regex:C1019("(「注\\d+」の場合)"; $診療行為名称等; 1; $pos; $len))
								$診療行為名称等:=Delete string:C232($診療行為名称等; $pos{1}; $len{1})
							End while 
							
							While (Match regex:C1019("(を算定している患者について.+場合)"; $診療行為名称等; 1; $pos; $len))
								$診療行為名称等:=Delete string:C232($診療行為名称等; $pos{1}; $len{1})
							End while 
							
							While (Match regex:C1019("(（(.*)使用）)"; $診療行為名称等; 1; $pos; $len))
								$診療行為名称等:=Delete string:C232($診療行為名称等; $pos{1}; $len{1})
							End while 
							
							While (Match regex:C1019("(の[イロハニホヘト])"; $診療行為名称等; 1; $pos; $len))
								$診療行為名称等:=Delete string:C232($診療行為名称等; $pos{1}; $len{1})
							End while 
							
							While (Match regex:C1019("(（入院外?）)"; $診療行為名称等; 1; $pos; $len))
								$診療行為名称等:=Delete string:C232($診療行為名称等; $pos{1}; $len{1})
							End while 
							
							$診療行為名称等:=str_trim($診療行為名称等)
							
							$o.診療行為名称:=$診療行為名称等
							
							$記載事項:=New shared object:C1526
							
							Use ($記載事項等)
								$記載事項等.push($記載事項)
							End use 
							
							Use ($記載事項)
								
								$記載事項.項番:=$o.項番
								$記載事項.区分:=$o.区分
								
								$記載事項.診療行為名称等:=$o.診療行為名称等
								$記載事項.診療行為名称:=$o.診療行為名称  //検索用
								$記載事項.記載事項:=New shared collection:C1527
								
								Case of 
									: ($診療行為名称等="")
										TRACE:C157  //XLSXにエラー（継続なし）
										$q:=""
									: ($診療行為名称等="薬剤等〈入院外分〉処方箋料")
										$q:="@処方箋料@"
									: ($診療行為名称等="薬剤等（入院外分）処方箋料")
										$q:="@処方箋料@"
									: ($診療行為名称等="往診料等")
										$q:="@往診料@"
									: ($診療行為名称等="外来栄養食事指導料")
										$q:="外来栄養食事指導料@"
									: ($診療行為名称等="腹腔鏡下胃縮小術（スリーブ状切除術によるもの）")
										$q:="腹腔鏡下胃縮小術（スリーブ状切除によるもの）@"
									: ($診療行為名称等="脳磁図１自発活動を測定するもの")
										$q:="@脳磁図（自発活動を測定するもの）@"
									: ($診療行為名称等="脳磁図２その他のもの")
										$q:="@脳磁図（その他のもの）@"
									: ($診療行為名称等="Ｌ型脂肪酸結合蛋白（ＬＦＡＢＰ）（尿）")
										$q:="@Ｌ型脂肪酸結合蛋白（Ｌ−ＦＡＢＰ）（尿）@"
									: ($診療行為名称等="筋電図検査２（誘発筋電図）")
										$q:="@誘発筋電図（神経伝導速度測定を含む）（１神経につき）@"
									: ($診療行為名称等="小腸内視鏡検査２（カプセル型内視鏡によるもの）")
										$q:="@小腸内視鏡検査（カプセル型内視鏡によるもの）@"
									: ($診療行為名称等="終夜睡眠ポリグラフィー３（１及び２以外の場合イ安全精度管理下で行うもの） ")
										$q:="@終夜睡眠ポリグラフィー（１及び２以外の場合）（安全精度管理下で行うもの）@"
									: ($診療行為名称等="呼吸心拍監視")
										$q:="@呼吸心拍監視@"
									: ($診療行為名称等="新生児心拍・呼吸監視")
										$q:="@新生児心拍・呼吸監視@"
									: ($診療行為名称等="カルジオスコープ（ハートスコープ）")
										$q:="@カルジオスコープ（ハートスコープ）@"
									: ($診療行為名称等="カルジオタコスコープ")
										$q:="@カルジオタコスコープ@"
									: ($診療行為名称等="光トポグラフィー２（抑うつ症状の鑑別診断の補助に使用するもの）")
										$q:="@光トポグラフィー（抑うつ症状の鑑別診断の補助に使用するもの）@"
									: ($診療行為名称等="光トポグラフィー１（脳外科手術の術前検査に使用するもの）")
										$q:="@光トポグラフィー（脳外科手術の術前検査に使用するもの）@"
									: ($診療行為名称等="肝硬度測定超音波エラストグラフィー")
										$q:="@超音波エラストグラフィー@"
									: ($診療行為名称等="認知機能検査その他の心理検査１（操作が容易なもの）（簡易なもの）")
										$q:="@認知機能検査その他の心理検査（操作が容易なもの）（簡易なもの）@"
									: ($診療行為名称等="検体検査判断料")
										$q:="@基本的検体検査判断料@"
									: ($診療行為名称等="乳腺腫瘍画像下ガイド下吸引術")
										$q:="@乳腺腫瘍画像ガイド下吸引術@"
									: ($診療行為名称等="排泄物、滲出物又は分泌物の細菌顕微鏡検査")
										$q:="@細菌顕微鏡検査@"
									: ($診療行為名称等="超音波検査２（断層撮影法（心臓超音波検査を除く。））（その他の場合）（胸腹部）")
										$q:="@超音波検査（断層撮影法（心臓超音波検査を除く。））（その他の場合）（胸腹部）@"
									: ($診療行為名称等="時間外加算１（手術）")
										$q:="@時間外加算１（手術）@"
									: ($診療行為名称等="放射線治療料")
										$q:="放射線治療管理料@"
									: ($診療行為名称等="神経ブロック加算")
										$q:="神経ブロック併施加算@"
									: ($診療行為名称等="染色体検査１")
										$q:="染色体検査（全ての費用を含む。）（ＦＩＳＨ法を用いた場合）@"
									: ($診療行為名称等="ウイルス・細菌核酸多項目同時検出（SARS-CoV-2を含む。）")
										$q:="ウイルス・細菌核酸多項目同時検出（ＳＡＲＳ−ＣｏＶ−２核酸検出・検査@"
									: ($診療行為名称等="超音波検査２（胸腹部）")
										$q:="超音波検査（断層撮影法（心臓超音波検査を除く。））（その他の場合）（胸腹部）@"
									: ($診療行為名称等="終夜睡眠ポリグラフィー３（１及び２以外の場合イ安全精度管理下で行うもの）")
										$q:="終夜睡眠ポリグラフィー（１及び２以外の場合）（安全精度管理下で行うもの）@"
									: ($診療行為名称等="小腸内視鏡検査３（カプセル型内視鏡によるもの）")
										$q:="小腸内視鏡検査（カプセル型内視鏡によるもの）@"
									: ($診療行為名称等="大腸内視鏡検査２（カプセル型内視鏡によるもの）")
										$q:="大腸内視鏡検査（カプセル型内視鏡によるもの）@"
									: ($診療行為名称等="低体温療法")
										$q:="マスク又は気管内挿管による閉鎖循環式全身麻酔１（人工心肺を用い低体温で行う心臓手術等）（@"
									: ($診療行為名称等="1,500グラム未満の乳幼児加算")
										$q:="@乳幼児加算@"
									: ($診療行為名称等="1,500グラム未満の新生児加算")
										$q:="@新生児加算@"
									: ($診療行為名称等="1,500グラム未満の小児加算")
										$q:="@小児加算@"
									: ($診療行為名称等="単心室症又は三尖弁閉鎖症手術")
										$q:="人工血管等再置換術加算（単心室症又は三尖弁閉鎖症手術）"
									Else 
										$q:=$診療行為名称等+"@"
								End case 
								
								Case of 
									: ($診療行為名称等="悪性腫瘍組織検査１")
										$c診療行為:=$rezept.診療行為.query("診療行為コード in :1"; $悪性腫瘍組織検査１)
										
									: ($診療行為名称等="悪性腫瘍遺伝子検査")
										$c診療行為:=$rezept.診療行為.query("診療行為コード in :1"; $悪性腫瘍遺伝子検査)
										
									: ($診療行為名称等="障害者施設等入院基本料")
										$c診療行為:=$rezept.診療行為.query("診療行為コード in :1"; $障害者施設等入院基本料)
										
									: ($診療行為名称等="一般病棟入院基本料")
										$c診療行為:=$rezept.診療行為.query("診療行為コード in :1"; $一般病棟入院基本料)
										
									: ($診療行為名称等="精神病棟入院基本料")
										$c診療行為:=$rezept.診療行為.query("診療行為コード in :1"; $精神病棟入院基本料)
										
									: ($診療行為名称等="特定機能病院入院基本料")
										$c診療行為:=$rezept.診療行為.query("診療行為コード in :1"; $特定機能病院入院基本料)
										
									: ($診療行為名称等="病理診断料")
										$c診療行為:=$rezept.診療行為.query("基本漢字名称 == :1 or 基本漢字名称 == :2"; "組織診断料@"; "細胞診断料@")
										
									: ($診療行為名称等="特定保険医療材料")
										
										$c特定器材:=$rezept.特定器材.query("項目.告示番号.区分番号 >= :1 and 項目.告示番号.区分番号 <= :2"; "1"; "209")
										$c診療行為:=$c特定器材.extract("特定器材コード"; "診療行為コード")
										
									: ($診療行為名称等="算定回数が複数月に１回のみとされている検査")
										$c診療行為:=$rezept.診療行為.query("基本漢字名称 in :1"; $算定回数が複数月に１回のみとされている検査)
										
									: ($診療行為名称等="ＭＥＴｅｘ14遺伝子検査")
										$c診療行為:=$rezept.診療行為.query("基本漢字名称 == :1 or 基本漢字名称 == :2"; "悪性腫瘍組織検査（処理が容易）（医薬品適応判定の補助等）（肺癌におけるＭＥＴｅｘ１４遺伝子検査（次世代シーケンシングを除く。））"; "悪性腫瘍組織検査（処理が複雑なもの）（肺癌におけるＭＥＴｅｘ１４遺伝子検査（次世代シーケンシング））")
										
									: ($診療行為名称等="在宅患者診療・指導料")
										$c診療行為:=$rezept.診療行為.query("基本漢字名称 == :1 or 基本漢字名称 == :2"; "在宅患者訪問診療料@"; "在宅患者訪問看護・指導料@")
									Else 
										$c診療行為:=$rezept.診療行為.query("基本漢字名称 == :1"; $q)
								End case 
								
							End use 
							
						End if 
						
						Case of 
							: ($c診療行為.length=0)
								
								//診療行為名で特定できない場合
								
								Case of 
									: ($診療行為名称等="ヘリコバクター・ピロリ感染の診断及び治療に関する取扱い")
									: ($診療行為名称等="特定保険医療材料")
									: ($診療行為名称等="「制限回数を超えて行う診療」に係るリハビリテーションを実施した場合")
									: ($診療行為名称等="写真診断")
									: ($診療行為名称等="コンピューター断層撮影診断")
									: ($診療行為名称等="施設入所者自己腹膜灌流薬剤料")
									: ($診療行為名称等="臨床研修病院入院診療加算")
									: ($診療行為名称等="がん拠点病院加算")
									: ($診療行為名称等="薬剤〈入院分〉")
									: ($診療行為名称等="回復期リハリビリテーション病棟入院料")
									: ($診療行為名称等=("救急患者として受け入れた患者が、処置室、手術室等において死亡した場合で、当該保険医療機関が救急医療を担う施設として確保することとされている専用病床（救急医療管理加算又"+"は救命救急入院料を算定する病床に限る。）に入院したものとみなす場合"))
									: ($診療行為名称等="在宅療養支援診療所、在宅療養支援病院から患者の紹介を受けて在宅療養指導管理を行う場合")
									: ($診療行為名称等="在宅植込型補助人工心臓􏰝非拍動流型の指導管理料")
									: ($診療行為名称等="在宅療養指導管理料のいずれかの所定点数に併せて特定保険医療材料のうち「皮膚欠損用創傷被覆材」又は「非固着性シリコンガーゼ」を支給した場合")
									: ($診療行為名称等="180日を超える期間通算対象入院料を算定する場合")
									: ($診療行為名称等="180日を超える期間通算対象入院料を算定している患者")
									: ($診療行為名称等="退院した患者に対して、当該退院月に、退院日に在宅療養指導管理料を算定した保険医療機関以外の保険医療機関において在宅療養指導管理料を算定した場合")
									: ($診療行為名称等="「制限回数を超えて行う診療」に係る精神科専門療法を実施した場合")
									: ($診療行為名称等="「制限回数を超えて行う診療」に係る検査を実施した場合")
									: ($診療行為名称等=("初診、再診又は在宅医療において、患者の診療を担う保険医の指示に基づき、当該保険医の診療日以外の日に訪問看護ステーション等の看護師等が、当該患者に対して点滴又は処置等を"+"実施した場合"))
									: ($診療行為名称等=("初診、再診又は在宅医療において、患者の診療を担う保険医の指示に基づき、当該保険医の診療日以外の日に訪問看護ステーション等の看護師等が、当該患者に対し、検査のための検体"+"採取等を実施した場合"))
									: ($診療行為名称等="1500グラム未満の小児加算")
									: ($診療行為名称等="1500グラム未満の乳幼児加算")
									: ($診療行為名称等="1500グラム未満の新生児加算")
										
										
									Else 
										TRACE:C157
								End case 
								
/*
								
*/
								
								If ($index=1)
									Use ($記載事項等)
										$記載事項等.pop($記載事項)
									End use 
								End if 
								
							Else 
								
								$記載事項_診療行為コード:=$c診療行為.extract("診療行為コード")
								
								If ($DEBUG_DIRECT_LINK)
									Use ($診療行為コード)
										For each ($code; $記載事項_診療行為コード)
											$診療行為コード[$code]:=$記載事項
										End for each 
									End use 
									Use ($診療行為名)
										$診療行為名[$診療行為名称等]:=$記載事項
									End use 
								End if 
								
								$codes:=$codes.combine($記載事項_診療行為コード)
								
								Use ($記載事項)
									
									If ($subIndex=1)
										
										$i:=0x0001
										
										$事項:=$values[3]
										
										If (Match regex:C1019("(?m)^（(.*)場合等?）$\\s*(?s)(.*)"; $事項; $i; $pos; $len))
											$qualifier:=Substring:C12($事項; $pos{1}; $len{1})
											$directive:=Substring:C12($事項; $pos{2}; $len{2})
											$指示:=New shared object:C1526("qualifier"; $qualifier; "message"; $directive)
										Else 
											$指示:=New shared object:C1526("message"; $事項)
										End if 
										
										$記載事項.記載事項.push($指示)
										$記載事項.診療行為コード:=$記載事項_診療行為コード.join(",")
										
										$steps:=New shared collection:C1527
										
										Use ($指示)
											$指示.steps:=$steps
										End use 
										
									End if 
									
									$コメント:=New shared collection:C1527
									$steps.push($コメント)
									
									$comment:=""
									
									$skip:=False:C215
									
									If ($values.length>4)
										
										Case of 
											: ($values[4]="算定日")
												//$算定日.push($記載事項.項番)
												$記載事項.記載事項.pop($指示)
												$skip:=True:C214
											: ($values[4]="メッセージ")
												//$メッセージ.push($記載事項.項番)
												$skip:=True:C214
												
												//: ($values[4]="検査の実施日")
												//$検査の実施日.push($記載事項.項番)
												
												//: ($values[4]="フリー")
												//$フリー.push($記載事項.項番)
												
											Else 
												$comment:=$values[4]
										End case 
										
									End if 
									
									If ($comment="")  //コメントコードなし
										$comment:="810000001"
									End if 
									
									If (Not:C34($skip))
										
										$i:=0x0001
										
										var $export : cs:C1710._export
										$export:=cs:C1710._export.new()
										
										While (Match regex:C1019("(\\d+)"; $comment; $i; $pos; $len))
											$code:=Substring:C12($comment; $pos{1}; $len{1})
											$i:=$pos{1}+$len{1}
											
											If ($export.コメント($code; $コメント))
												//コメントコード
											Else 
												//診療行為コード
												$export.診療行為($code; $コメント)
											End if 
										End while 
										
									End if 
									
								End use 
								
						End case 
						
					End if 
					
				End for each 
				
				If (Not:C34($DEBUG_DIRECT_LINK))
					
					$記載事項_診療行為コード:=$codes.distinct()
					
					Use ($診療行為コード)
						For each ($code; $記載事項_診療行為コード)
							
							$records:=$記載事項等.query("診療行為コード == :1"; "@"+$code+"@")
							
							$count:=0
							
							For each ($record; $records)
								$count:=$count+$record.記載事項.sum("steps.length")
							End for each 
							
							If ($count#0)
								$診療行為コード[$code]:=$records
							Else   //算定日のみ
								
							End if 
							
						End for each 
					End use 
					
				End if 
				
				If ($DEBUG_CREATE_DATA)
					VARIABLE TO BLOB:C532($EXPORT; $blob)
					$file:=Folder:C1567("/RESOURCES/").file($dataClassName+".data")
					$file.setContent($blob)
				End if 
				
			End if 
			
		End if 
		
	End if 
Else 
	
	If (Storage:C1525[$dataClassName]=Null:C1517)
		
		$EXPORT:=New shared object:C1526
		
		Use (Storage:C1525)
			Storage:C1525[$dataClassName]:=$EXPORT
		End use 
		
		$file:=Folder:C1567("/RESOURCES/").file($dataClassName+".data")
		
		If ($file.exists)
			
			$blob:=$file.getContent()
			
			C_OBJECT:C1216($object)
			BLOB TO VARIABLE:C533($blob; $object)
			$object:=OB Copy:C1225($object; ck shared:K85:29; $EXPORT)
			
			Use ($EXPORT)
				$EXPORT.診療行為コード:=$object.診療行為コード
				
				If ($object.診療行為名#Null:C1517)
					$EXPORT.診療行為名:=$object.診療行為名
				End if 
				
				$EXPORT.記載事項等:=$object.記載事項等
				//$EXPORT.フリー:=$object.フリー
				//$EXPORT.検査の実施日:=$object.検査の実施日
				//$EXPORT.算定日:=$object.算定日
				//$EXPORT.メッセージ:=$object.メッセージ
			End use 
			
		End if 
	Else 
		$EXPORT:=Storage:C1525[$dataClassName]
	End if 
	
End if 

$0:=$EXPORT